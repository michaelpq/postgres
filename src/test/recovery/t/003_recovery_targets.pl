
# Copyright (c) 2021-2026, PostgreSQL Global Development Group

# Test for recovery targets: name, timestamp, XID
use strict;
use warnings FATAL => 'all';
use PostgreSQL::Test::Cluster;
use PostgreSQL::Test::Utils;
use Test::More;
use Time::HiRes qw(usleep);

# Create and test a standby from given backup, with a certain recovery target.
# Choose $until_lsn later than the transaction commit that causes the row
# count to reach $num_rows, yet not later than the recovery target.
sub test_recovery_standby
{
	local $Test::Builder::Level = $Test::Builder::Level + 1;

	my $test_name = shift;
	my $node_name = shift;
	my $node_primary = shift;
	my $recovery_params = shift;
	my $num_rows = shift;
	my $until_lsn = shift;

	my $node_standby = PostgreSQL::Test::Cluster->new($node_name);
	$node_standby->init_from_backup($node_primary, 'my_backup',
		has_restoring => 1);

	foreach my $param_item (@$recovery_params)
	{
		$node_standby->append_conf('postgresql.conf', qq($param_item));
	}

	$node_standby->start;

	# Wait until standby has replayed enough data
	my $caughtup_query =
	  "SELECT '$until_lsn'::pg_lsn <= pg_last_wal_replay_lsn()";
	$node_standby->poll_query_until('postgres', $caughtup_query)
	  or die "Timed out while waiting for standby to catch up";

	# Create some content on primary and check its presence in standby
	my $result =
	  $node_standby->safe_psql('postgres', "SELECT count(*) FROM tab_int");
	is($result, qq($num_rows), "check standby content for $test_name");

	# Stop standby node
	$node_standby->teardown_node;

	return;
}

# Start a standby with the given pg_ctl --options string and verify that
# the standby reaches the given LSN and row count.  Used to exercise
# scenarios that require the postmaster command line to receive multiple
# "-c name=value" instances of the same GUC, which postgresql.conf cannot
# express because ProcessConfigFile collapses duplicate keys.
sub test_recovery_standby_with_options
{
	local $Test::Builder::Level = $Test::Builder::Level + 1;

	my $test_name = shift;
	my $node_name = shift;
	my $node_primary = shift;
	my $options = shift;
	my $num_rows = shift;
	my $until_lsn = shift;

	my $node_standby = PostgreSQL::Test::Cluster->new($node_name);
	$node_standby->init_from_backup($node_primary, 'my_backup',
		has_restoring => 1);

	my $res = run_log(
		[
			'pg_ctl',
			'--pgdata' => $node_standby->data_dir,
			'--log' => $node_standby->logfile,
			'--options' => $options,
			'start',
		]);
	ok($res, "server starts for $test_name");

	$node_standby->poll_query_until('postgres',
		"SELECT '$until_lsn'::pg_lsn <= pg_last_wal_replay_lsn()")
	  or die "Timed out while waiting for standby to catch up";

	my $count = $node_standby->safe_psql('postgres',
		"SELECT count(*) FROM tab_int");
	is($count, qq($num_rows), "check standby content for $test_name");

	$node_standby->teardown_node;

	return;
}

# Initialize primary node
my $node_primary = PostgreSQL::Test::Cluster->new('primary');
$node_primary->init(has_archiving => 1, allows_streaming => 1);

# Bump the transaction ID epoch.  This is useful to stress the portability
# of recovery_target_xid parsing.
system_or_bail('pg_resetwal', '--epoch' => '1', $node_primary->data_dir);

# Start it
$node_primary->start;

# Create data before taking the backup, aimed at testing
# recovery_target = 'immediate'
$node_primary->safe_psql('postgres',
	"CREATE TABLE tab_int AS SELECT generate_series(1,1000) AS a");
my $lsn1 =
  $node_primary->safe_psql('postgres', "SELECT pg_current_wal_lsn();");

# Take backup from which all operations will be run
$node_primary->backup('my_backup');

# Insert some data with used as a replay reference, with a recovery
# target TXID.
$node_primary->safe_psql('postgres',
	"INSERT INTO tab_int VALUES (generate_series(1001,2000))");
my $ret = $node_primary->safe_psql('postgres',
	"SELECT pg_current_wal_lsn(), pg_current_xact_id();");
my ($lsn2, $recovery_txid) = split /\|/, $ret;

# More data, with recovery target timestamp
$node_primary->safe_psql('postgres',
	"INSERT INTO tab_int VALUES (generate_series(2001,3000))");
my $lsn3 =
  $node_primary->safe_psql('postgres', "SELECT pg_current_wal_lsn();");
my $recovery_time = $node_primary->safe_psql('postgres', "SELECT now()");

# Even more data, this time with a recovery target name
$node_primary->safe_psql('postgres',
	"INSERT INTO tab_int VALUES (generate_series(3001,4000))");
my $recovery_name = "my_target";
my $lsn4 =
  $node_primary->safe_psql('postgres', "SELECT pg_current_wal_lsn();");
$node_primary->safe_psql('postgres',
	"SELECT pg_create_restore_point('$recovery_name');");

# And now for a recovery target LSN
$node_primary->safe_psql('postgres',
	"INSERT INTO tab_int VALUES (generate_series(4001,5000))");
my $lsn5 = my $recovery_lsn =
  $node_primary->safe_psql('postgres', "SELECT pg_current_wal_lsn()");

$node_primary->safe_psql('postgres',
	"INSERT INTO tab_int VALUES (generate_series(5001,6000))");

# Force archiving of WAL file
$node_primary->safe_psql('postgres', "SELECT pg_switch_wal()");

# LSN after the final 6000-row insert and WAL switch.  The set-then-clear case
# below has no recovery target and replays all WAL, so it polls on this instead
# of $lsn5, which would race the 5001-6000 rows.
my $lsn6 =
  $node_primary->safe_psql('postgres', "SELECT pg_current_wal_lsn()");

# Test recovery targets
my @recovery_params = ("recovery_target = 'immediate'");
test_recovery_standby('immediate target',
	'standby_1', $node_primary, \@recovery_params, "1000", $lsn1);
@recovery_params = ("recovery_target_xid = '$recovery_txid'");
test_recovery_standby('XID', 'standby_2', $node_primary, \@recovery_params,
	"2000", $lsn2);
@recovery_params = ("recovery_target_time = '$recovery_time'");
test_recovery_standby('time', 'standby_3', $node_primary, \@recovery_params,
	"3000", $lsn3);
@recovery_params = ("recovery_target_name = '$recovery_name'");
test_recovery_standby('name', 'standby_4', $node_primary, \@recovery_params,
	"4000", $lsn4);
@recovery_params = ("recovery_target_lsn = '$recovery_lsn'");
test_recovery_standby('LSN', 'standby_5', $node_primary, \@recovery_params,
	"5000", $lsn5);

# Regression: empty-string for one recovery_target_* GUC must not clobber
# another non-empty target.  Setting recovery_target_xid + recovery_target_time
# = '' must recover to the xid, not run as no-target recovery.
@recovery_params = (
	"recovery_target_xid = '$recovery_txid'",
	"recovery_target_time = ''");
test_recovery_standby('xid with empty time GUC',
	'standby_xid_empty_time', $node_primary, \@recovery_params,
	"2000", $lsn2);

# Multiple targets
#
# Multiple conflicting non-empty settings are rejected.  Setting the same
# parameter twice is allowed (last value wins), and an empty string is a no-op
# that does not clear another GUC's target.  Conflicts are detected at every
# server start by DetermineRecoveryTargetType().

@recovery_params = (
	"recovery_target_name = '$recovery_name'",
	"recovery_target_name = ''",
	"recovery_target_time = '$recovery_time'");
test_recovery_standby('multiple overriding settings',
	'standby_6', $node_primary, \@recovery_params, "3000", $lsn3);

# Check behavior when recovery ends before target is reached

my $node_standby = PostgreSQL::Test::Cluster->new('standby_8');
$node_standby->init_from_backup(
	$node_primary, 'my_backup',
	has_restoring => 1,
	standby => 0);
$node_standby->append_conf('postgresql.conf',
	"recovery_target_name = 'does_not_exist'");

run_log(
	[
		'pg_ctl',
		'--pgdata' => $node_standby->data_dir,
		'--log' => $node_standby->logfile,
		'start',
	]);

# wait for postgres to terminate
foreach my $i (0 .. 10 * $PostgreSQL::Test::Utils::timeout_default)
{
	last if !-f $node_standby->data_dir . '/postmaster.pid';
	usleep(100_000);
}
my $logfile = slurp_file($node_standby->logfile());
like(
	$logfile,
	qr/FATAL: .* recovery ended before configured recovery target was reached/,
	'recovery end before target reached is a fatal error');

# Conflicts are rejected at every startup, even without recovery.signal.
# init_from_backup without has_restoring creates no recovery.signal, so this
# cluster would otherwise start as a plain primary; the conflict must still be
# caught.
my $node_no_signal = PostgreSQL::Test::Cluster->new('multi_target_no_signal');
$node_no_signal->init_from_backup($node_primary, 'my_backup');
$node_no_signal->append_conf(
	'postgresql.conf', "recovery_target_name = '$recovery_name'
recovery_target_time = '$recovery_time'");

my $res_no_signal = run_log(
	[
		'pg_ctl',
		'--pgdata' => $node_no_signal->data_dir,
		'--log' => $node_no_signal->logfile,
		'start',
	]);
ok(!$res_no_signal,
	'server fails to start with conflicting recovery targets and no recovery.signal');

my $logfile_no_signal = slurp_file($node_no_signal->logfile());
like(
	$logfile_no_signal,
	qr/multiple recovery targets specified/,
	'expected error message logged without recovery.signal');
like(
	$logfile_no_signal,
	qr/Only one recovery target can be set\.  Parameters set: "recovery_target_name", "recovery_target_time"/,
	'errdetail lists the set parameters in order without recovery.signal');
unlike(
	$logfile_no_signal,
	qr/Parameters set:[^\n]*=/,
	'errdetail does not echo parameter values without recovery.signal');

# Same-GUC set-then-clear: setting a recovery_target_* GUC and then setting the
# same GUC to an empty string leaves no target, so recovery runs to the end of
# WAL.  Duplicate keys collapse in postgresql.conf, so "pg_ctl --options" passes
# both assignments on the postmaster command line.
test_recovery_standby_with_options(
	'recovery_target_xid set then cleared',
	'standby_xid_set_clear', $node_primary,
	"-c recovery_target_xid=$recovery_txid -c recovery_target_xid=",
	"6000", $lsn6);

# Set recovery_target_xid, then set and clear recovery_target_name.  Only the
# xid remains, so recovery must stop at it rather than running to the end of WAL
# (a competing target that is set then cleared must not strand the first one).
test_recovery_standby_with_options(
	'recovery target preserved when a competing one is set then cleared',
	'standby_clobber_clear', $node_primary,
	"-c recovery_target_xid=$recovery_txid -c recovery_target_name=$recovery_name -c recovery_target_name=",
	"2000", $lsn2);

# Invalid recovery_target_timeline tests
my ($result, $stdout, $stderr) = $node_primary->psql('postgres',
	"ALTER SYSTEM SET recovery_target_timeline TO 'bogus'");
like(
	$stderr,
	qr/is not a valid number/,
	"invalid recovery_target_timeline (bogus value)");

($result, $stdout, $stderr) = $node_primary->psql('postgres',
	"ALTER SYSTEM SET recovery_target_timeline TO '0'");
like(
	$stderr,
	qr/must be between 1 and 4294967295/,
	"invalid recovery_target_timeline (lower bound check)");

($result, $stdout, $stderr) = $node_primary->psql('postgres',
	"ALTER SYSTEM SET recovery_target_timeline TO '4294967296'");
like(
	$stderr,
	qr/must be between 1 and 4294967295/,
	"invalid recovery_target_timeline (upper bound check)");

# Invalid recovery_target_xid tests
($result, $stdout, $stderr) = $node_primary->psql('postgres',
	"ALTER SYSTEM SET recovery_target_xid TO 'bogus'");
like(
	$stderr,
	qr/is not a valid number/,
	"invalid recovery_target_xid (bogus value)");

($result, $stdout, $stderr) = $node_primary->psql('postgres',
	"ALTER SYSTEM SET recovery_target_xid TO '-1'");
like(
	$stderr,
	qr/is not a valid number/,
	"invalid recovery_target_xid (negative)");

($result, $stdout, $stderr) = $node_primary->psql('postgres',
	"ALTER SYSTEM SET recovery_target_xid TO '0'");
like(
	$stderr,
	qr/without epoch must be greater than or equal to 3/,
	"invalid recovery_target_xid (lower bound check)");

done_testing();
